/*
 * Copyright (c) 2017 Ronald D. Kurr kurr@jvmguy.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'

defaultTasks << 'clean'
defaultTasks << 'build'

dependencies {
    compile "org.codehaus.groovy:groovy-all::indy"
}

compileGroovy {
    groovyOptions.configurationScript = file( "$rootDir/gradle/groovyCompilerConfiguration.groovy" )
}

compileGroovy.groovyOptions.optimizationOptions['indy'] = true
compileTestGroovy.groovyOptions.optimizationOptions['indy'] = true

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']
compileTestGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']

ext.sharedManifest = manifest {
    attributes( 'Implementation-Title': project.name, 'Implementation-Version': project.version )
}

test {
    useJUnit {
        includeCategories 'org.kurron.categories.UnitTest'
    }
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    reports.html.setDestination( file( "$buildDir/reports/unitTests" ) )

    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "512m"
}

task allIntegrationTests( type: Test, dependsOn: [build, startIntegrationTestResources] ) {
    group 'Verification'
    description = 'Runs the all integration tests.'
    useJUnit {
        includeCategories 'org.kurron.categories.InboundIntegrationTest', 'org.kurron.categories.OutboundIntegrationTest'
    }
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    reports.html.setDestination( file( "$buildDir/reports/integrationTests" ) )
    // copy into forked child process
    doFirst {
        systemProperties['spring.profiles.active'] = System.properties['spring.profiles.active'] ?: 'default'

        systemProperties['spring.rabbitmq.port'] = rabbitPort
        logger.quiet "Port for RabbitMQ is ${systemProperties['spring.rabbitmq.port']}"
        systemProperties['spring.rabbitmq.host'] = rabbitHost
        logger.quiet "Host for RabbitMQ is ${systemProperties['spring.rabbitmq.host']}"

        systemProperties['spring.data.mongodb.port'] = mongoPort
        logger.quiet "Port for MongoDB is ${systemProperties['spring.data.mongodb.port']}"
        systemProperties['spring.data.mongodb.host'] = mongoHost
        logger.quiet "Host for MongoDB is ${systemProperties['spring.data.mongodb.host']}"

        // When running inside Docker, the JVM has a hard time figuring out out what HOME is so we set it
        // by hand.  Here, we propagate that value to the test JVM.
        systemProperties['user.home'] = System.properties['user.home'] ?: 'UNKNOWN!'
        logger.quiet "JVM system property user.home is set to ${systemProperties['user.home']}"
    }
}

if ( 'true' == runIntegrationTests ) {
    defaultTasks += ['allIntegrationTests']
    allIntegrationTests.finalizedBy destroyIntegrationTestResources
}
